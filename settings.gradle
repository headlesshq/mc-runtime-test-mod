
pluginManagement {
    repositories {
        mavenCentral()
        maven {
            url = "https://maven.neoforged.net/releases"
        }
        maven {
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            url = "https://maven.fabricmc.net/"
        }
        maven {
            url = "https://maven.wagyourtail.xyz/releases"
        }
        maven {
            url 'https://3arthqu4ke.github.io/maven'
        }
        maven {
            url = "https://maven.wagyourtail.xyz/snapshots"
        }
        gradlePluginPortal() {
            content {
                excludeGroup("org.apache.logging.log4j")
            }
        }
    }
}

def loadVersions() {
    // Collect and clean version names from the "versions" directory
    def versions = fileTree("versions").files.name.collect { it.replaceAll("\\.properties", "") }

    // Sort by semantic version
    versions = versions.sort { a, b ->
        def partsA = a.tokenize('.')*.toInteger()
        def partsB = b.tokenize('.')*.toInteger()

        int comparison = partsA[0] <=> partsB[0]
        if (comparison != 0) return comparison

        comparison = partsA[1] <=> partsB[1]
        if (comparison != 0) return comparison

        return partsA[2] <=> partsB[2]
    }

    gradle.ext.mc_versions = versions
}

loadVersions()

include 'api'
rootProject.name = 'mc-runtime-test'
