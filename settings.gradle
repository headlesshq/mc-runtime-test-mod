
pluginManagement {
    repositories {
        mavenCentral()
        maven {
            url = "https://maven.neoforged.net/releases"
        }
        maven {
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            url = "https://maven.fabricmc.net/"
        }
        maven {
            url = "https://maven.wagyourtail.xyz/releases"
        }
        maven {
            url 'https://3arthqu4ke.github.io/maven'
        }
        maven {
            url = "https://maven.wagyourtail.xyz/snapshots"
        }
        gradlePluginPortal() {
            content {
                excludeGroup("org.apache.logging.log4j")
            }
        }
    }
}

def loadVersions() {
    def versions = fileTree("versions").files.name
    for (int i = 0; i < versions.size(); i++) {
        versions[i] = versions[i].replaceAll("\\.properties", "")
    }
    // sort by semantic version
    versions = versions.sort { a, b ->
        def partsA = a.tokenize('.')*.toInteger()
        def partsB = b.tokenize('.')*.toInteger()

        int comparison = partsA[0] <=> partsB[0]
        if (comparison != 0) return comparison

        comparison = partsA[1] <=> partsB[1]
        if (comparison != 0) return comparison

        return partsA[2] <=> partsB[2]
    }

    var mcVersion = minecraft_version
    var mcIndex = versions.indexOf(minecraft_version)
    def props = new Properties()
    props.load(new FileInputStream("$rootDir/versions/" + "$mcVersion" + ".properties"))

    props.each { prop ->
        gradle.ext.set(prop.key, prop.value)
    }

    gradle.ext.mc_versions = versions
    gradle.ext.mc_index = mcIndex
}

loadVersions()

include 'api'
rootProject.name = 'mc-runtime-test'
