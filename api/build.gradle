plugins {
    id 'java'
    id 'maven-publish'
    id 'systems.manifold.manifold-gradle-plugin'
}

group 'io.github.headlesshq'
version "$project_version"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("systems.manifold:manifold-preprocessor:2024.1.37")
}

compileJava {
    if (JavaVersion.current().isJava9Compatible()) {
        options.compilerArgs.addAll(['--release', '8'])
    }
}

afterEvaluate {
    publishing {
        publications {
            create(name.toLowerCase(), MavenPublication) {
                groupId    = group
                artifactId = archivesBaseName.toLowerCase()
                version    = project.version  // or just version

                from(components.java)
            }
        }

        repositories {
            if (System.getenv('DEPLOY_TO_GITHUB_PACKAGES_URL') == null) {
                maven {
                    name = 'BuildDirMaven'
                    url = rootProject.projectDir.toPath().parent.resolve('build').resolve('maven')
                }
            } else {
                maven {
                    name = 'GithubPagesMaven'
                    url = System.getenv('DEPLOY_TO_GITHUB_PACKAGES_URL')
                    credentials {
                        username = System.getenv('GITHUB_ACTOR')
                        password = System.getenv('GITHUB_TOKEN')
                    }
                }
            }
        }
    }
}
