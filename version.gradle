buildscript {
    repositories {
        mavenCentral()
        maven {
            url = "https://maven.neoforged.net/releases"
        }
        maven {
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            url = "https://maven.fabricmc.net/"
        }
        maven {
            url = "https://maven.wagyourtail.xyz/releases"
        }
        maven {
            url = "https://3arthqu4ke.github.io/maven"
        }
        maven {
            url = "https://maven.wagyourtail.xyz/snapshots"
        }
        maven {
            url = "https://repo.spongepowered.org/maven"
        }
        maven {
            url = "https://impactdevelopment.github.io/maven/"
        }
        gradlePluginPortal() {
            content {
                excludeGroup("org.apache.logging.log4j")
            }
        }
    }
    dependencies {
        classpath "com.github.johnrengelman:shadow:8.1.1"
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

// Extract version name from subproject name
def verId = project.name
println "Configuring subproject: ${project.path}, version=${verId}"

def propsFile = new File(rootDir, "versions/${verId}/gradle.properties")
if (!propsFile.exists()) {
    throw new GradleException("Properties file not found: ${propsFile}")
}

project.gradle.ext.getProperties().each { prop ->
    rootProject.ext.set(prop.key, prop.value)
}

def setupPreprocessors(List<String> mcVersions, String currentVersion) {
    StringBuilder sb = new StringBuilder();
    mcVersions.eachWithIndex { ver, i ->
        String verStr = ver.replace(".", "_");
        sb.append("MC_${verStr}=${i}\n")
        if (ver == currentVersion) {
            sb.append("MC_VER=${i}\n")
        }
    }
    new File(projectDir, "build.properties").text = sb.toString()
}

def props = new Properties()
props.load(new FileInputStream(propsFile))

def currentMinecraftVersion = props.getProperty('minecraft_version')
def currentLexforgeVersion = props.getProperty('lexforge_version')
def currentNeoForgeVersion = props.getProperty('neoforge_version')
def currentLoaders = props.getProperty('loaders')?.split(',')*.trim()
def currentMappings = props.getProperty('mappings')

println "'minecraft_version' = ${currentMinecraftVersion}"
//println "'java_version' = ${currentJavaVersion}"
println "'lexforge_version' = ${currentLexforgeVersion}"
println "'neoforge_version' = ${currentNeoForgeVersion}"
println "'loaders' = ${currentLoaders}"
println "'mappings' = ${currentMappings}"

setupPreprocessors(rootProject.mc_versions, currentMinecraftVersion)

def buildVersion = "${currentMinecraftVersion}-${project(':api').project_version}"

sourceSets { // TODO: make everything conditional
    fabric {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
    lexforge {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
    if (currentLoaders.contains('neoforge')) {
        neoforge {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

unimined.minecraft {
    version currentMinecraftVersion
    mappings {
        currentMappings.split(',').each { mapping ->
            String[] split = mapping.split(':')
            String name = split[0]
            if (name == 'searge') {
                searge()
            } else if (name == 'mcp') {
                mcp("stable", split[1])
            } else if (name == 'mojmap') {
                mojmap()
                // intermediary()
                // yarn(1)
                devFallbackNamespace "mojmap"
            }
        }
    }
    defaultRemapJar = false
}

unimined.minecraft(sourceSets.fabric) {
    combineWith(sourceSets.main)
    if (currentLoaders.contains('legacyfabric')) {
        legacyFabric {
            loader rootProject.fabric_version
        }
    } else {
        fabric {
            loader rootProject.fabric_version
        }
    }
    defaultRemapJar = true
}

if (currentLoaders.contains('neoforge')) {
    unimined.minecraft(sourceSets.neoforge) {
        combineWith(sourceSets.main)

        neoForge {
            loader currentNeoForgeVersion
            mixinConfig 'mc_runtime_test.mixins.json'
        }

        minecraftRemapper.config {
            ignoreConflicts(true)
        }

        defaultRemapJar = true
    }
}

unimined.minecraft(sourceSets.lexforge) {
    combineWith(sourceSets.main)

    minecraftForge {
        loader currentLexforgeVersion
        mixinConfig 'mc_runtime_test.mixins.json'
    }

    defaultRemapJar = true
}

configurations {
    mainImplementation
    implementation {
        extendsFrom lexforgeImplementation
        extendsFrom fabricImplementation
    }

    jarLibs
    implementation.extendsFrom jarLibs
}

sourceSets.main {
    compileClasspath += configurations.mainImplementation
    runtimeClasspath += configurations.mainImplementation
}

dependencies {
    annotationProcessor("systems.manifold:manifold-preprocessor:${rootProject.manifold_version}")
    lexforgeAnnotationProcessor("systems.manifold:manifold-preprocessor:${rootProject.manifold_version}")
    fabricAnnotationProcessor("systems.manifold:manifold-preprocessor:${rootProject.manifold_version}")
    if (project.loaders.contains('neoforge')) {
        neoforgeAnnotationProcessor("systems.manifold:manifold-preprocessor:${rootProject.manifold_version}")
    }

    if (currentLoaders.contains('legacyfabric')) {
        implementation 'net.minecraft:launchwrapper:1.12'
        jarLibs 'com.github.ImpactDevelopment:SimpleTweaker:1.2'
        // ewww, legaacy minecraft needs this
        // TODO: this could cause problems with 1.12.2 mods using newer versions
        //  they could just rename their jar to aaaaa-modname.jar though?
        jarLibs('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
            exclude module: 'launchwrapper'
            exclude module: 'guava'
            exclude module: 'gson'
            exclude module: 'commons-io'
        }
    } else {
        implementation('org.spongepowered:mixin:0.8.5') {
            exclude module: 'launchwrapper'
            exclude module: 'guava'
            exclude module: 'gson'
            exclude module: 'commons-io'
        }
    }

    // yes, I actually want this at runtime to use assertions!
    jarLibs 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    jarLibs project(':api')
}

def mc_platforms = ['Fabric', 'Lexforge']
if (currentLoaders.contains('neoforge')) {
    mc_platforms = ['Fabric', 'Neoforge', 'Lexforge']
}

for (String platform_capitalized : mc_platforms) {
    def platform = platform_capitalized.toLowerCase()
    def remapJarTask = tasks.named("remap${platform_capitalized}Jar", AbstractArchiveTask).get()
    def shadowTask = tasks.register("${platform}ShadowJar", ShadowJar) {
        dependsOn(remapJarTask)
        it.group = 'build'
        it.archiveClassifier = "${platform}-release"
        from remapJarTask.outputs
        it.configurations += [ project.configurations.jarLibs ]
        // Mixin is signed
        exclude 'META-INF/*.RSA'
        exclude 'META-INF/*.SF'
        exclude "**/module-info.class"
    }

    tasks.named('build') { finalizedBy(shadowTask) }
}

jar {
    enabled = false
}

tasks.withType(org.gradle.jvm.tasks.Jar).configureEach {
    from("LICENSE") {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        rename { "${it}_${archivesBaseName}" }
    }

    manifest {
        if (currentLoaders.contains('legacyfabric')) {
            attributes(
                'Implementation-Title': 'MC-Runtime-Test',
                'TweakClass': 'io.github.headlesshq.mcrt.tweaker.McRuntimeTestTweaker',
                'MixinConfigs': "mc_runtime_test.mixins.json",
                'Implementation-Version': buildVersion,
            )
        } else {
            attributes(
                'Implementation-Title': 'MC-Runtime-Test',
                'MixinConfigs': "mc_runtime_test.mixins.json",
                'Implementation-Version': buildVersion,
            )
        }
    }
}

def expansions = [
    version    : buildVersion,
    mc_version : currentMinecraftVersion,
]

processFabricResources {
    filesMatching("fabric.mod.json") {
        expand expansions
    }
}

processLexforgeResources {
    filesMatching("META-INF/mods.toml") {
        expand expansions
    }

    filesMatching("mcmod.info") {
        expand expansions
    }
}

if (project.loaders.contains('neoforge')) {
    processNeoforgeResources {
        // TODO: this is legacy neoforge, newer neoforge needs the other toml, check if its ok to leave this in
        filesMatching("META-INF/mods.toml") {
            expand expansions
        }

        filesMatching("META-INF/neoforge.mods.toml") {
            expand expansions
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            "${name.toLowerCase()}"(MavenPublication) {
                ((MavenPublication) it).groupId "${group}"
                ((MavenPublication) it).artifactId "${archivesBaseName.toLowerCase()}"
                ((MavenPublication) it).version "${version}"
                from components.java
                for (String platform: mc_platforms) {
                    String platform_lower = platform.toLowerCase()
                    artifact tasks.named("${platform_lower}Jar").get()
                    artifact tasks.named("remap${platform}Jar").get()
                    artifact tasks.named("${platform_lower}ShadowJar").get()
                }
            }
        }

        repositories {
            if (System.getenv('DEPLOY_TO_GITHUB_PACKAGES_URL') == null) {
                maven {
                    name = 'BuildDirMaven'
                    url = rootProject.projectDir.toPath().parent.resolve('build').resolve('maven')
                }
            } else {
                maven {
                    name = 'GithubPagesMaven'
                    url = System.getenv('DEPLOY_TO_GITHUB_PACKAGES_URL')
                    credentials {
                        username = System.getenv('GITHUB_USER')
                        password = System.getenv('GITHUB_TOKEN')
                    }
                }
            }
        }
    }
}

